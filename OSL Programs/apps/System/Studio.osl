import as "glass" from "packages"

file "use_type" "txt"

permission "request" "file admin"
window "show"
"Studio".roturConnect()

change_focus = true
current_tab_target = 0
removed = false

save "Studio" "set_directory"

if "recent_files.json".saveExists() (
  recent = "recent_files.json".saveGet()
) else (
  save "recent_files.json" "set" []
  recent = []
)
recent_2 = recent.trim(1,10)
save "recent_files.json" "set" recent_2

tabs = []
if passed_data == "" (
  tabs.append("> Welcome")
) else (
  change_focus = true
  tabs.append(passed_data)
)
tabs_out = tabs
sidebar_items = 0

current_tab = tabs[1]

dragbox = [[2,2,40,0],[-2,2,-100,-40]]

def "draw_buttons" (
  loc -2 2 -50 -20
  square 80 20 10 : c#window_colour
  loc -2 2 -20 -20
  
  icon "close" 0.6 : c#c_text
  if onclick (
    window "stop"
  )
  
  change_x -30
  icon "down" 0.6 : c#c_text
  if onclick (
    window "minimise"
  )

  change_x -30
  icon "maximise" 0.6 : c#c_text
  if onclick (
    window "maximise"
  )
)

def "add_tab" "data" (
  tabs.append(data)
  change_focus = true
  current_tab = data
)

def "insert_tab" "index, data" (
  tabs.insert(index,data)
  change_focus = true
  current_tab = data
)

def "draw_tabs" (
  loc 2 9999 10 0
  
  c c_text
  x = frame.left - 2.5
  if tabs_out != tabs (
    window "refresh_bg"
    tabs_out = tabs
  )
  
  each i tab tabs (
    tw = 0
    if tab.startsWith("> ") (
      tab_name = tab.trim(3,-1)
      file = false
    ) else (
      file "open" tab "onlyaccess"
      tab_name = fileGet(2) ++ fileGet(1)
      file = true
    )
    
    l = tab_name.len * 9 + 25
    if file "l += 20"
    tw += l + 25
    goto x + (tw / 2) 0
    
    square l + 10 20 10 : c#window_colour
    if file (
      change_x -10
      square l - 10 20 10 0 1
      change_x 10
      file "interact"
      if data == "open" (
        current_tab = tab
        change_focus = true
      )
      if data == "dragging" (
        removed = true
        change_focus = true
      )
    ) else (
      if onclick (
        current_tab = tab
        change_focus = true
      )
    )
    change_x l / -2
    if file (
      change_x 5
      file "render" 0.7
      change_x 15
    )
    c tab == current_tab ? c_text c_seco
    text tab_name 9
    icon "close" 0.5 : chx#20
    if onclick (
      removed = true
    )
    if file_dragging (
      change_x 17
      if dist(x_position,y_position,mouse_x,mouse_y) < 40 (
        square 0 30 2 : c#c_text
        current_tab_target = i + 1
      )
      change_x -17
    )
    if removed (
      window "refresh_bg"
      change_focus = true
      if i > 1 (
        prev = tabs[i - 1]
      ) else (
        prev = tabs[i + 1]
      )
      tabs_out.delete(i)
      current_tab = prev
      if tabs_out.len == 0 (
        tabs = tabs_out
        add_tab "> Welcome"
        tabs_out = tabs
      )
      removed = false
    )
    x += tw
  )
  tabs = tabs_out
  icon "add" 0.6 : chx#30 c#c_text
  if onclick (
    add_tab "> Welcome"
  )
  dragbox[1][3] = x + frame.right + 80
)

sharing_with = null

start = true
mainloop:
theme = user.theme
c_prim = theme.primary
c_seco = theme.secondary
c_tert = theme.tertiary
c_text = theme.text
window_colour = theme.background

if new_transmit (
  if window.children.contains(transmit_source) (
    add_tab transmit_data
  )
  new_transmit = false
)

if change_focus and start.not (
  change_focus = false
)
start = false

if mouse_down.not "can = true"

w = window

goto 0 window.top - 20
glass:frame w.left w.top w.right w.bottom;frame "clear"

frame w.left + 50 w.top w.right - 60 w.top - 40
draw_tabs
window "set_dragbox" dragbox
frame "clear"

right = w.right - (preview_open ? 400 0)

goto 22.5 -17.5
square w.width - 65 w.height - 55 10 : c#window_colour

if file_dropped != 0 (
  if current_tab_target == 0 (
    current_tab_target = tabs.len + 1
  )
  insert_tab current_tab_target file_dropped
  current_tab_target = 0
  file_dropped = 0
)

if current_studio_page == "Home" (
  w = window_width / 2 - 40
  w2 = w - 40
  loc 2 2 70 -75
  text "New File" 8 : c#c_text

  loc 999 2 20 -75
  text "Recent Files" 8
  
  loc 2 2 w / 2 + 50 -290
  square w2 30 10 : c#c_seco
  if onclick (
    file "pick"
  )
  change_x w2 / -2 + 10
  text "Open File" 9 : c#c_text

  loc 2 2 w / 2 + 50 -125
  square w2 30 10 : c#c_prim
  input w2 - 10 25 "name" "hello world.txt" 10 c_text
  change_y -55
  if input_name.matchregex("/[^.]+\.[^.]+/gm") (
    square w2 30 10 : c#c_seco cursor#pointer
    if mouse_touching (
      square w2 30 15 : c#global_accent
      square w2 30 10 : c#c_seco
    )
    if selected_input == "name" and "enter".onpress or onclick (
      file "download" input_name
      add_tab data
    )
    change_x w2 / -2 + 15
  ) else (
    square w2 30 10 : c#c_prim
    change_x w2 / -2 + 15
  )
  c c_text
  icon "Add" 0.5
  text "Create New File" 8 : chx#20

  c c_prim
  frame 10 window.top - 93 window.right - 10 window.bottom recent_2.len * 55
  w = frame_width - 20
  for count recent_2.len (
    y = count * -55 + 25 + scroll_y
    loc 999 2 0 y
    square w 30 10 : c#c_prim cursor#pointer
    cur = recent_2[count]
    if mouse_touching (
      square w 30 15 : c#c_seco
      square w 30 10 : c#c_prim
      if onclick (
        add_tab cur.toStr()
      )
    )
    change w / -2 + 15
    file "open" cur.str "onlyaccess"
    if loaded_file !== 0 (
      file "render" 1
      loc 2 2 60 y
      file "get" 2
      name = data
      file "get" 1
      text name ++ data 9 : c#c_text
    ) else (
      recent.delete(count)
      save "recent_files.json" "set" recent
      count --
      recent2 = recent.trim(1,10)
    )
  )
  frame "clear"
  
  draw_buttons
)

sharing_with.destr

if new_network_data (
  if packet.username == sharing_with (
    systemvars "setlist" "App // Data" packet.data
  )
  new_network_data = false
)

if change_focus (
  if current_tab == "> Welcome" (
    current_studio_page = "Home"
  ) else (
    current_studio_page = current_tab
  )
)

draw_buttons
loc 2 2 25 -25
sidebar_items = 0
